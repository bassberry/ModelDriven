-- @path ScreenModel=/ScreenModel/model/screenModel.ecore
-- @path WorkflowModel=/WorkflowModel/model/workflowModel.ecore

module Workflow2ModelTransformation;
create OUT : ScreenModel from IN : WorkflowModel;

helper context WorkflowModel!Zustand def: hasEingabe(): Boolean =
	if not self.datenobjektEingabe.oclIsUndefined() then 
		if self.datenobjektAusgabe.oclIsUndefined() then
			true
		else
			false
		endif
	else 
		false
	endif;

helper context WorkflowModel!Zustand def: hasAusgabe(): Boolean =
	if not self.datenobjektAusgabe.oclIsUndefined() then 
		if self.datenobjektEingabe.oclIsUndefined() then
			true
		else
			false
		endif
	else 
		false
	endif;

helper context WorkflowModel!Zustand def: hasEinUndAusgabe(): Boolean =
	if not self.datenobjektAusgabe.oclIsUndefined() then 
		if not self.datenobjektEingabe.oclIsUndefined() then
			true
		else
			false
		endif
	else 
		false
	endif;

rule Workflow2ScreenModel {
	from workflow: WorkflowModel!Workflow
	to screenModel: ScreenModel!ScreenModel (
		name <- workflow.name,
		seite <- workflow.zustand,
		datenobjekt <- workflow.datenobjekt,
		datenstruktur <- workflow.datenstruktur
	)
}
rule Datenobjekt2Datenobjekt {
	from workflowDatenobjekt: WorkflowModel!Datenobjekt
	to screenDatenobjekt: ScreenModel!Datenobjekt (
		name <- workflowDatenobjekt.name,
		datenstruktur <- workflowDatenobjekt.datenstruktur
	)
}
rule Datenstruktur2Datenstruktur {
	from workflowDatenstruktur: WorkflowModel!Datenstruktur
	to screenDatenstruktur: ScreenModel!Datenstruktur (
		name <- workflowDatenstruktur.name,
		attribute <- workflowDatenstruktur.attribute
	)
}
rule Attribut2Attribut {
	from workflowAttribut: WorkflowModel!Attribut
	to screenAttribut: ScreenModel!Attribut (
		name <- workflowAttribut.name,
		datentyp <- workflowAttribut.datentyp
	)
}

--Seite mit Datenein- und ausgabe
rule Zustand2SeiteEinUndAusgabe {
	from zustand: WorkflowModel!Zustand (zustand.hasEinUndAusgabe())
	to 	seite: ScreenModel!Seite (
			pageName <- zustand.pageName,
			button <- zustand.uebergang,
			dateneingabe <- eingabe,
			datenausgabe <- ausgabe
		),
		eingabe: ScreenModel!Dateneingabe (
			datenobjekt <- zustand.datenobjektEingabe,
			dateneingabefeld <- zustand.datenobjektEingabe.datenstruktur.attribute->collect(e | 
				thisModule.Attribut2Eingabe(e)
			)
		)
		ausgabe: ScreenModel!Datenausgabe (
			datenobjekt <- zustand.datenobjektAusgabe,
			datenausgabefeld <- zustand.datenobjektAusgabe->collect(f | 
				f.datenstruktur.attribute->collect(e | thisModule.Attribut2Ausgabe(e)))
		)
}
rule Zustand2SeiteEin {
	from zustand: WorkflowModel!Zustand (zustand.hasEingabe())
	to 	seite: ScreenModel!Seite (
			pageName <- zustand.pageName,
			button <- zustand.uebergang,
			dateneingabe <- eingabe
		),
		eingabe: ScreenModel!Dateneingabe (
			datenobjekt <- zustand.datenobjektEingabe,
			dateneingabefeld <- zustand.datenobjektEingabe.datenstruktur.attribute->collect(e | 
				thisModule.Attribut2Eingabe(e)
			)
		)
}
rule Zustand2SeiteAusgabe {
	from zustand: WorkflowModel!Zustand (zustand.hasAusgabe())
	to 	seite: ScreenModel!Seite (
			pageName <- zustand.pageName,
			button <- zustand.uebergang,
			datenausgabe <- ausgabe
		),
		ausgabe: ScreenModel!Datenausgabe (
			datenobjekt <- zustand.datenobjektAusgabe,
			datenausgabefeld <- zustand.datenobjektAusgabe->collect(f | 
				f.datenstruktur.attribute->collect(e | thisModule.Attribut2Ausgabe(e)))
		)
}
rule Uebergang2Button {
	from uebergang: WorkflowModel!Uebergang
	to button: ScreenModel!Button (
		displayText <- uebergang.name,
		seite <- uebergang.zustand
	)
}
--Lazy rules fÃ¼r jeden Attributtyp
lazy rule Attribut2Eingabe {
	from attribut: WorkflowModel!Attribut 
	to eingabefeld: ScreenModel!Dateneingabefeld (
			eingabefeld <- ScreenModelEingabefeld,
			textfeld <- ScreenModelTextfeld
		),
		ScreenModelEingabefeld: ScreenModel!Eingabefeld (
			attribut <- attribut
		),
		ScreenModelTextfeld: ScreenModel!Textfeld (
			displayText <-attribut.name+':'
		)
}
lazy rule Attribut2Ausgabe {
	from attribut: WorkflowModel!Attribut 
	to ausgabefeld: ScreenModel!Datenausgabefeld (
			ausgabefeld <- ScreenModelAusgabefeld,
			textfeld <- ScreenModelTextfeld
		),
		ScreenModelAusgabefeld: ScreenModel!Ausgabefeld (
			attribut <- attribut
		),
		ScreenModelTextfeld: ScreenModel!Textfeld (
			displayText <- attribut.name+':'
		)
}



